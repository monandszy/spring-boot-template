plugins {
  application
  pmd
  jacoco
  alias(libs.plugins.spring.boot)
  alias(libs.plugins.spring.management)
  id("com.diffplug.spotless") version "6.25.0"
}

group = "code"
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

repositories {
  mavenCentral()
  maven { url = uri("https://repo.spring.io/release") }
  maven { url = uri("https://repo.spring.io/milestone") }
}

version = "1.0.0-SNAPSHOT"

dependencies {
  implementation(libs.spring.modulith)
  implementation(libs.spring.web)
  implementation(libs.spring.validation)
  implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0")
  implementation(libs.mapstruct)

  compileOnly(libs.lombok)
  annotationProcessor(libs.lombok)
  annotationProcessor(libs.bundles.mapstruct.annotation)
  testImplementation(libs.lombok)
  testAnnotationProcessor(libs.lombok)
  testAnnotationProcessor(libs.bundles.mapstruct.annotation)

  testImplementation(libs.junit.jupiter)
  testRuntimeOnly(libs.junit.platform)
  testImplementation(libs.bundles.spring.test)
}

dependencyManagement {
  imports {
    mavenBom("org.springframework.modulith:spring-modulith-bom:1.2.5")
  }
}

spotless {
  java {
    target("src/**/*.java")
      googleJavaFormat()
  }
}

tasks {
  compileJava {
    options.encoding = "UTF-8"
  }
  compileTestJava {
    options.encoding = "UTF-8"
  }
  bootJar {
    archiveFileName = "${project.name}-${version}.${archiveExtension.get()}"
  }
  jar {
    enabled = false
  }

  test {
    useJUnitPlatform()
    testLogging {
      events("passed", "skipped", "failed")
    }
  }

  check {
    dependsOn(spotlessApply)
    dependsOn(spotlessCheck)
    dependsOn(pmdMain)
    dependsOn(pmdTest)
    dependsOn(jacocoTestReport)
  }

  pmd {
    toolVersion = "7.4.0"
    isConsoleOutput = false
    isIgnoreFailures = true
    rulesMinimumPriority = 5
    ruleSets = listOf("category/java/errorprone.xml", "category/java/bestpractices.xml")
    pmdMain {
      doLast {
        val reportPath = layout.buildDirectory.file("reports/pmd/main.html").get().asFile
        println("PmdMain report: file://${reportPath.toURI().path}")
      }
    }
    pmdTest {
      doLast {
        val reportPath = layout.buildDirectory.file("reports/pmd/test.html").get().asFile
        println("PmdTest report: file://${reportPath.toURI().path}")
      }
    }
  }

  jacoco {
    jacocoTestReport {
      reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir("reports/jacoco")
      }
      doLast {
        val reportPath = layout.buildDirectory.file("reports/jacoco/index.html").get().asFile
        println("Jacoco report: file://${reportPath.toURI().path}")
      }
      dependsOn(test)
    }
  }
}